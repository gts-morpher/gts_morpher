grammar uk.ac.kcl.inf.XDsmlCompose with org.eclipse.xtext.common.Terminals

generate xDsmlCompose "http://www.ac.uk/kcl/inf/XDsmlCompose"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/emf/2011/Henshin" as gts

GTSMapping:
	(autoComplete ?= "auto-complete" (uniqueCompletion ?= "unique")?)? "map" "{"
		"from" source = GTSSpecification
		"to" target = GTSSpecification 
	 
		typeMapping = TypeGraphMapping
		(behaviourMapping = BehaviourMapping)?
	"}"
;

GTSSpecification:
	(interface_mapping ?= "interface_of")?
	"{"
		gts = GTSSelection	
	"}"
;

GTSSelection:
	GTSLiteral |
	GTSFamilyChoice
;

GTSLiteral:
	"metamodel" ":" metamodel = [ecore::EPackage | STRING]
	("behaviour" ":" behaviour = [gts::Module | STRING])?
;

GTSFamilyChoice:
	"family" ":" "{"
		root = GTSLiteral
		"transformers" ":" transformers = [gts::Module | STRING]
	"}"
	
	"using" "["
		transformationSteps = UnitCallList
	"]"
;

UnitCallList:
	steps += UnitCall ("," steps += UnitCall)*
;

UnitCall:
  unit = [gts::Unit | ID] "(" (params = UnitParameterList)? ")"
;

UnitParameterList:
	parameters += UnitParameter ("," parameters += UnitParameter)*
;

UnitParameter:
	EObjectReferenceParameter | StringParameter
;

EObjectReferenceParameter:
	qualifiedName = QualifiedName
;

StringParameter:
	value = STRING
;

TypeGraphMapping:
	"type_mapping" "{"
		mappings += TypeMapping+ 
	"}"
;

TypeMapping:
	ClassMapping | ReferenceMapping
;

ClassMapping:
	"class" source = [ecore::EClassifier | QualifiedName] "=>" target = [ecore::EClassifier | QualifiedName]
;

ReferenceMapping:
	"reference" source = [ecore::EReference | QualifiedName] "=>" target = [ecore::EReference | QualifiedName]
;

BehaviourMapping:
	"behaviour_mapping" "{"
		mappings += RuleMapping+
	"}"
;

RuleMapping:
	"rule" source=[gts::Rule | ID] "to" target=[gts::Rule | ID] "{"
		element_mappings += RuleElementMapping+
	"}"
;

RuleElementMapping:
	ObjectMapping | LinkMapping
;

ObjectMapping:
	"object" source = [gts::Node | ID] "=>" target = [gts::Node | ID]
;

LinkMapping:
	"link" source = [gts::Edge | LinkName] "=>" target = [gts::Edge | LinkName]
;

LinkName :
	"[" ID "->" ID ":" ID "]"
;

QualifiedName:
	ID ('.' ID)*
;