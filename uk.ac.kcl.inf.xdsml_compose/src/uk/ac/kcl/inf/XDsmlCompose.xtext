grammar uk.ac.kcl.inf.XDsmlCompose with org.eclipse.xtext.common.Terminals

generate xDsmlCompose "http://www.ac.uk/kcl/inf/XDsmlCompose"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/emf/2011/Henshin" as henshin

GTSMapping:
	(autoComplete ?= "auto-complete" (uniqueCompletion ?= "unique")?)? "map" "{"
		"from" source = GTSSpecification
		"to" target = GTSSpecification 
	 
		typeMapping = TypeGraphMapping
		(behaviourMapping = BehaviourMapping)?
	"}"
;

GTSSpecification:
	"{"
		"metamodel" ":" metamodel = [ecore::EPackage | STRING]
		("behaviour" ":" behaviour = [henshin::Module | STRING])?
	"}"
;

TypeGraphMapping:
	"type_mapping" "{"
		mappings += TypeMapping+ 
	"}"
;

TypeMapping:
	ClassMapping | ReferenceMapping
;

ClassMapping:
	"class" source = [ecore::EClassifier | QualifiedName] "=>" target = [ecore::EClassifier | QualifiedName]
;

ReferenceMapping:
	"reference" source = [ecore::EReference | QualifiedName] "=>" target = [ecore::EReference | QualifiedName]
;

BehaviourMapping:
	"behaviour_mapping"
;

QualifiedName:
	ID ('.' ID)*
;